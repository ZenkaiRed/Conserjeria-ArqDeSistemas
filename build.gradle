plugins {
    id 'idea'
    id 'java'
    id('io.ebean') version '13.25.2'
    id('com.github.ben-manes.versions') version '0.48.0'
    id('com.google.protobuf') version '0.9.4'
    id('com.github.johnrengelman.shadow') version '8.1.1'
}

group = 'cl.ucn.disc.as'
version = '0.0.1'

repositories {
    mavenLocal()
    mavenCentral()
}

protobuf{
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.12"
    }
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.60.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}

dependencies {
    // junit
    testImplementation platform('org.junit:junit-bom:5.10.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // lombok
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    // ebean
    testImplementation 'io.ebean:ebean-test:13.25.2'

    // ebean
    implementation 'io.ebean:ebean:13.25.2'
    implementation 'io.ebean:ebean-ddl-generator:13.25.2'
    annotationProcessor 'io.ebean:querybean-generator:13.25.2'

    // database
    implementation 'org.xerial:sqlite-jdbc:3.44.1.0'
    implementation "org.mariadb.jdbc:mariadb-java-client:3.3.1"


    // json
    implementation 'com.google.code.gson:gson:2.10.1'

    // web: javalin
    implementation 'io.javalin:javalin:5.6.3'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // logger
    implementation 'ch.qos.logback:logback-core:1.4.14'
    implementation 'ch.qos.logback:logback-classic:1.4.14'

    // commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.14.0'

    implementation 'com.github.javafaker:javafaker:1.0.2'

    // grpc
    runtimeOnly "io.grpc:grpc-netty-shaded:1.60.0"
    implementation "io.grpc:grpc-protobuf:1.60.0"
    implementation "io.grpc:grpc-stub:1.60.0"
    implementation "javax.annotation:javax.annotation-api:1.3.2"


}

ebean {
    debugLevel = 9 //0 - 9
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
}

idea {
    module {
        sourceDirs += file('src/generated/main/java')
        sourceDirs += file('src/generated/main/grpc')
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'cl.ucn.disc.as.Main'
    }
}

shadowJar {
    mergeServiceFiles()
}
